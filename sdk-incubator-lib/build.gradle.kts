plugins {
    id("com.android.library")
    id("org.jetbrains.kotlin.android")
    id("zcash-sdk.android-conventions")

    id("org.jetbrains.dokka")

    id("wtf.emulator.gradle")
    id("zcash-sdk.emulator-wtf-conventions")

    id("maven-publish")
    id("signing")
    id("zcash-sdk.publishing-conventions")
}

mavenPublishing {
    coordinates(
        artifactId = "zcash-android-sdk-incubator"
    )
}

// Selects which sync engine WalletCoordinator drives. Exactly one of the two source directories
// supplies `engineSynchronizerFactory`, so the engine choice is resolved at SDK build time and no
// runtime flag survives into the published artifact.
val isSlipstreamEnabled = project.property("IS_SLIPSTREAM_ENABLED").toString().toBoolean()

android {
    namespace = "cash.z.ecc.android.sdk.incubator"

    useLibrary("android.test.runner")

    sourceSets.getByName("main") {
        java.srcDir(if (isSlipstreamEnabled) "src/engineSlipstream/java" else "src/engineDefault/java")
    }
    if (isSlipstreamEnabled) {
        sourceSets.getByName("test") {
            java.srcDir("src/testEngineSlipstream/java")
        }
    }

    defaultConfig {
        consumerProguardFiles("proguard-consumer.txt")
    }

    buildTypes {
        getByName("debug").apply {
            isMinifyEnabled = false
        }
        getByName("release").apply {
            isMinifyEnabled = project.property("IS_MINIFY_SDK_ENABLED").toString().toBoolean()
            proguardFiles.addAll(
                listOf(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    File("proguard-project.txt")
                )
            )
        }
        create("benchmark") {
            // We provide the extra benchmark build type just for benchmarking purposes
            initWith(buildTypes.getByName("release"))
            matchingFallbacks += listOf("release")
        }
    }
}

/**
 * The gift card redeemer's engine seam ([cash.z.ecc.android.sdk.GiftCardRedeemers]) implements
 * sdk-lib's `internal` card wallet interface, the way slipstream-lib uses sdk-lib's `internal`
 * declarations. Registering sdk-lib as a Kotlin friend module gives that access at compile time
 * without widening sdk-lib's public API; the build directory prefix covers every variant and
 * compilation (main and unit test).
 */
val sdkLibBuildDir = project(":sdk-lib").layout.buildDirectory
tasks.withType<org.jetbrains.kotlin.gradle.tasks.KotlinCompile>().configureEach {
    friendPaths.from(sdkLibBuildDir)
}

tasks.dokkaHtml.configure {
    dokkaSourceSets {
        configureEach {
            outputDirectory.set(file("build/docs/rtd"))
            displayName.set("Zcash Android SDK")
            includes.from("packages.md")
        }
    }
}

dependencies {
    // Deliberately the string notation: the `projects.slipstreamLib` typesafe accessor does not
    // exist when the module is not included, which would break configuration of flag-off builds.
    if (isSlipstreamEnabled) {
        implementation(project(":slipstream-lib"))
    }

    implementation(projects.sdkLib)
    implementation(libs.bip39)

    implementation(libs.androidx.annotation)

    implementation(libs.kotlinx.datetime)

    // Architecture Components: Lifecycle
    // implementation(libs.androidx.lifecycle.runtime)
    // implementation(libs.androidx.lifecycle.common)

    // Kotlin
    implementation(libs.kotlin.stdlib)
    implementation(libs.kotlinx.coroutines.core)
    implementation(libs.kotlinx.coroutines.android)

    // Tests
    testImplementation(libs.kotlin.reflect)
    testImplementation(libs.kotlin.test)
    testImplementation(libs.bundles.junit)
    testImplementation(libs.mockito.junit)
    testImplementation(projects.backendLib)

    androidTestImplementation(libs.androidx.test.runner)
    androidTestImplementation(libs.androidx.test.junit)
    androidTestImplementation(libs.androidx.test.core)
    androidTestImplementation(libs.kotlin.test)
    androidTestImplementation(libs.kotlinx.coroutines.test)

    // sample mnemonic plugin
    androidTestImplementation(libs.zcashwalletplgn)
}
